(* JDK regexes *)

simple-character = #"[^\\\[\]\(\)\{\}\.*+?]"

special-character = #"[\[\]\(\)\{\}\.*+?]"

octal-codepoint = <"0"> #"[0-3]?[0-7]?[0-7]"

hex-codepoint = <"x"> ( #"[a-fA-F0-9]{2,4}" | <"{"> #"[a-fA-F0-9]+" <"}"> )

codepoint = octal-codepoint | hex-codepoint

control-character = <"c"> simple-character

named-character = #"[tnrfae]"
(* tab, newline, return, form-feed, bell (alert), escape *)

negative-named-character-class = <"P{"> #"[^\}]+" <"}">

positive-named-character-class = <"p{"> #"[^\}]+" <"}">

named-character-class = positive-named-character-class | negative-named-character-class

escaped-character = <"\\"> ( codepoint | control-character | named-character | named-character-class | special-character)

character = escaped-character | simple-character

character-range = character <"-"> character

positive-character-class = ( character-range / character ) +

negative-character-class = <"^"> ( character-range / character ) +

character-class-intersection = ( character-range / character ) +
(* Yes this is the same as positive-character-class *)

character-class-difference = <"^"> ( character-range / character ) +
(* Yes this is the same as negative-character-class *)

character-class-arithmetic = ( <"&&["> ( character-class-intersection | character-class-difference ) <"]"> ) +

character-class = <"["> ( negative-character-class / positive-character-class ) character-class-arithmetic ? <"]">

shorthand-repetition = atom ("?" | "*" | "+")

bounded-repetition = atom ( <"{"> #"\d+" ( <","> #"\d+" ) ? <"}"> )

unbounded-repetition = atom ( <"{"> #"\d+" <","> <"}"> )

parenthetical = <"("> alternation <")">

atom = character-class | character | parenthetical

simple-repetition = unbounded-repetition / bounded-repetition / shorthand-repetition

possessive-repetition = simple-repetition <"+">

relucant-repetition = simple-repetition <"?">

repetition = possessive-repetition / relucant-repetition / simple-repetition / atom

concatenation = ( repetition | atom ) concatenation ?

alternation = concatenation ( <"|"> alternation ) ?

pattern = alternation
