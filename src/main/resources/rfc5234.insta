(*
   An instaparse grammar parsing rfc5234 grammars.

    Modified from https://tools.ietf.org/html/rfc5234
    AKA https://www.ietf.org/rfc/rfc5234.txt
*)

rulelist =  ( rule | addition | <c-nl> ) *

rule =  rulename <"="> elements <c-nl>
(* continues if next line starts with white space *)

addition = rulename <"=/"> elements <c-nl>
(* additions add more alternations to an existing rule *)

rulename =  #"[a-zA-Z][a-zA-Z0-9\-]*"

elements =  alternation

(* c-wsp =  WSP | ( c-nl WSP )

   is dead and was removed
 *)

c-nl =  comment | <CRLF>
(* comment or newline *)

comment =  #";[^\n\r]*" <CRLF> 

alternation =  concatenation ( <c-nl> * <"/"> alternation ) ?

concatenation =  repetition concatenation ?

repetition =  repeat ? element

repeat = range-repeat / specific-repeat
(* Either a range repetition or a specific repetition may occur *)

range-repeat =  DIGIT ? "*" DIGIT ?
(* Both the upper and lower bound are specified as being optional *)

specific-repeat = DIGIT


element =  rulename | group | option | char-val | num-val | prose-val

group =  <"("> alternation <")">

option =  <"["> alternation <"]">

char-val = <"\""> #"[^\"]*" <"\"">
(* originally DQUOTE (%x20-21 | %x23-7E)+ DQUOTE
   quoted string of SP and VCHAR without DQUOTE
*)

num-val =  <"%"> ( bin | dec | hex )

bin =  <"b"> ( range / pair / val )
(* series of concatenated bit values or single ONEOF range *)

dec =  <"d"> ( range / pair / val )

hex =  <"x"> ( range / pair / val )

range = #"[0-9A-F]+" <"-"> #"[0-9A-F]+"

pair = #"[0-9A-F]+" <"."> #"[0-9A-F]+"

val = #"[0-9A-F]+"

(* Note: originally range, pair and val were specified as alternations per encoding base.  For
   grammar simplicity, this was reduced to generic range and pair terminals which may be decoded at
   the level of the bin/dec/hex node. This does make the parse grammar laxer as the Instaparse
   parser will now consider any valid numeric string legal while reading, so the tree transformer
   must reject incorrectly encoded literals.
*)

prose-val =  <"<"> #"[^>]*" <">">
(* bracketed string of SP and VCHAR without angles prose description, to be used as last resort *)

CR =  "\r"
(* carriage return *)

CRLF =  CR ? LF
(* Internet standard newline
   Note: Originally the CR was required. I made it optional to support Unix encoded files.
*)

DIGIT =  #"[0-9]+"
(* 0-9 *)

LF =  "\n"
(* linefeed *)

(* LWSP =  ( WSP | CRLF WSP ) *

   Dead terminal and removed due to referencing removed WSP

   Use of this linear-white-space rule
   permits lines containing only white
   space that are no longer legal in
   mail headers and have caused
   interoperability problems in other
   contexts.

   Do not use when defining mail
   headers and use with caution in
   other contexts.
*)

OCTET =  #"[\x00-\xFF]"
(* 8 bits of data *)

SP =  " "
