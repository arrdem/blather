(* An Instaparse grammar for Lark gramars as documented here

   https://github.com/erezsh/lark/blob/master/docs/reference.md

   Modified from the RFC 5234 grammar to preserve the maximum of naming
 *)

grammar = ( rule | pragma | <c-nl> ) *

c-nl = comment | <CRLF>

comment = #"^//[^\n\r]*" <CRLF>

rule = rulename <":"> elements

pragma = import | ignore
(* Lark has "pragmas", which are directives to the grammar compiler *)

import = <"%import"> symbol alias ?

ignore = <"%ignore"> terminal

rulename = rule-tag ? symbol

rule-tag = "?" | "!"

symbol = #"[a-zA-Z_][a-zA-Z0-9\-_\\.]*"

elements = alternation

alternation = concatenation ( <c-nl> * <"|"> alternation ) ?

concatenation = repetition + alias ?
(* FIXME: can I get away without + here somehow? *)

alias = <"->"> symbol
(* Aliases may be used to assign names in the grammar compiler to various entities *)

repetition = element repeat ?

repeat = zero-or-more | one-or-more | zero-or-one

zero-or-more = <"*">

one-or-more = <"+">

zero-or-one = <"?">

element = group | option-group | terminal

terminal = symbol | char-val | re-pattern

group = <"("> alternation <")">

option-group = <"["> alternation <"]">

char-val = <"\""> #"([^\"]|\\\")*" <"\"">

re-pattern = <"/"> #"([^/\n\r]|\\\/)*" <"/">
(* Lark uses almost C# style regexes *)

CR = "\r"
(* carriage return *)

LF = "\n"
(* linefeed *)

CRLF = CR ? LF
(* Guarding against Windows and Unix file encodings *)